OUTPUT_ARCH(arm)
ENTRY("_kzstart")

MEMORY
{
	/* o: ORIGIN, l:LENGTH */
	ramall(rwx)	: o = 0x00000000, l = 0x1c000000 /* 512-64MB */
	ram(rwx)	: o = 0x00008000, l = 0x1bff8000 /* entry point(0x8000) - end(0x1c000000) */

	heap(rw)	: o = 0x10000000, l = 0x02000000 /* 32MB heap of RAM */

	softvec(rw)	: o = 0x1a000000, l = 0x00000040 /* top of RAM */
	userstack(rw)	: o = 0x1b000000, l = 0x00000000 /* fiq - 16MB */
	bootstack(rw)	: o = 0x1c000000, l = 0x00000000 /* svc - 16MB */
	intrstack(rw)	: o = 0x1c000000, l = 0x00000000 /* end of RAM */
}

SECTIONS
{
	.text : {
		_text_start = . ;
		*(.text)
		_etext = . ;
	} > ram

	. = ALIGN(4);

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = . ;
	} > ram

	. = ALIGN(4);

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > ram

	. = ALIGN(4);

	.bss : {
		_bss_start = . ;
		__bss_start__ = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
		__bss_end__ = . ;
	} > ram

	. = ALIGN(4);
	_end = . ;

	.freearea : {
		_freearea = .;
	} > ram

	. = ALIGN(4);

	.softvec : {
		_softvec = .;
	} > softvec

	. = ALIGN(4);

	.heap : {
			_heap = .;
	} > heap

	. = ALIGN(4);

	.userstack : {
		_userstack = .;
	} > userstack

	. = ALIGN(4);

	.bootstack : {
		_bootstack = .;
	} > bootstack

	. = ALIGN(4);

	.intrstack : {
		_intrstack = .;
	} > intrstack
	
	. = ALIGN(4);
}
